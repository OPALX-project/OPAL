OPTION, PSDUMPFREQ = 1000;   // 6d data written every 300 time steps (h5).
OPTION, STATDUMPFREQ = 1000;  // Beam Stats written every 10 time steps (stat).
OPTION, BOUNDPDESTROYFQ=10; // Delete lost particles, if any
OPTION, AUTOPHASE=4;        // Autophase is on, and phase of max energy
                            // gain will be found automatically for cavities.
OPTION, VERSION=10900;

REAL REPARTFREQ=1000000;

Title, string="UNDULATOR";

//----------------------------------------------------------------------------
//Global Parameters

// REAL rf_freq             = 1.3e9;     //RF frequency. (Hz)
REAL n_particles         = 1024;      //Number of particles in simulation.
REAL beam_bunch_charge   = 1.846e8 * 1.6e-19;      //Charge of bunch. (C)

//Initial Momentum Calculation
REAL gamma   = 100.41; 
REAL beta    = sqrt(1-(1/gamma^2));
REAL P0      = gamma*beta*EMASS;    //inital z momentum

//Printing initial energy and momentum to terminal output.
value , {gamma, P0};


// Undulator

UND: UNDULATOR, L = 9.0, ELEMEDGE = 1.19814e-4, K = 1.417, LAMBDA = .03, FNAME = "reduced.job";

DRIVE: Line = (UND);

//----------------------------------------------------------------------------
// INITIAL DISTRIBUTION
//
// Flattop distribution. 
// SIGMAX/Y:      RMS radius of transverse beam size (Laser radius in m). 
// TRISE/FALL:    Rise time/fall time in longitudinal direction (s).
// TPULSEFWHM:    FWHM in longitudinal direction (s).
// CUTOFFLONG:    Longitudinal cuttoff in units of sigma.
// NBIN:          Number of energy bins to use during emission.
// DEBIN:         Defines when to combine bins (Min energy difference in KeV). 
// EMISSIONSTEPS: Number of steps during emssion. 
//                Emission time step is adjusted to fit this number.
// EKIN:          Kinetic energy of electrons at emission (eV). 
// ELASER:        Energy of laser (eV). 
// W:             Photocathode work functioin (eV). 
// FE:            Fermi energy of photocathode (eV). 
// CATHTEMP:      Operating temperature of photocathode (K). 

// Note, ELASER, W, FE, and CATHTEMP are used for the NONEQUIL emission model.
// These values are not necessary when using other models.

// Note, If you want a Gaussian, in the longitudinal direction:
//      TRISE/FALL = 1.6869*simgar
//      Sigmar     = FWHM / 2.35 (this equation is only valid for 1.3 GHz)

Dist: DISTRIBUTION, TYPE = FLATTOP,
        SIGMAX = 0.00075,
        SIGMAY = 0.00075,
        TRISE = 6.0e-12,       
        TFALL = 6.0e-12,       
        TPULSEFWHM = 20.0e-12, 
        CUTOFFLONG = 4.0,
        NBIN = 5,
        EMISSIONSTEPS = 100,
        EMISSIONMODEL = ASTRA,
        EKIN = 0.55,           
        EMITTED = True,        
        WRITETOFILE = False;    //Saves the distribution to a text file

// Note on emission time step: FWHM pulse width divided by emission 
// steps gives the time step for the emissions process. 
// i.e 20.0e-12 / 100 gives a time step of 2e-13 (s) during emission. 
// This is the not the same as the time step used in rest of the file. 

//----------------------------------------------------------------------------
// Define Field solvers
// The mesh sizes should be a factor of 2 
// for most efficient space charge calculation.

FS_SC: Fieldsolver, FSTYPE = FFT, 
            MX = 8, MY = 8, MT = 8, // SC grid size is 8^3
            PARFFTX = true, 
            PARFFTY = true, 
            PARFFTT = true,  // parallel in the z direction only
            BCFFTX = open, 
            BCFFTY = open, 
            BCFFTT = open,
            BBOXINCR = 1, 
            GREENSF = INTEGRATED;

//----------------------------------------------------------------------------
// Electron Beam Definition

// BEAM1:  BEAM, PARTICLE = ELECTRON, pc = P0, NPART = n_particles,
        BFREQ = rf_freq, BCURRENT = beam_bunch_charge * rf_freq, CHARGE = -1;
total_mithra_time = 3e-8;
BEAM1:  BEAM, PARTICLE = ELECTRON, pc = P0, NPART = n_particles,
	BCURRENT = beam_bunch_charge / total_mithra_time , CHARGE = -1;

//----------------------------------------------------------------------------
// Simulate the beamline using TRACK and RUN.

TRACK, LINE = DRIVE, BEAM = BEAM1, MAXSTEPS = 1000000, DT = 3.E-10, ZSTOP= 10.0 ;
RUN, METHOD = "PARALLEL-T", BEAM = BEAM1, 
    FIELDSOLVER = FS_SC, DISTRIBUTION = Dist;
ENDTRACK;

Quit;
