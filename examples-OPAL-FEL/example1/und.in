OPTION, PSDUMPFREQ = 100000000;   // 6d data written every 300 time steps (h5).
OPTION, STATDUMPFREQ = 1000;  // Beam Stats written every 10 time steps (stat).
OPTION, BOUNDPDESTROYFQ=10; // Delete lost particles, if any
OPTION, AUTOPHASE=4;        // Autophase is on, and phase of max energy
                            // gain will be found automatically for cavities.
OPTION, VERSION=10900;


REAL REPARTFREQ=1000000;

Title, string="UNDULATOR_ModulatedBunch";

//----------------------------------------------------------------------------
//Global Parameters

REAL rf_freq             = 1.0;     //RF frequency. (Hz)
REAL n_particles         = 1e5;      //Number of particles in simulation.
REAL beam_bunch_charge   =  .3e-09;      //Charge of bunch. (C)

//Initial Momentum Calculation
REAL gamma   = 44e-3 / EMASS; // Energy in GeV
REAL beta    = sqrt(1-(1/gamma^2));
REAL P0      = gamma*beta*EMASS;    //inital z momentum

//Printing initial energy and momentum to terminal output.
value , {gamma, P0};

// Undulator of length 85cm + 17cm fringe distance at the entrance
// Parameters for undulator and full-wave solver:
UND: UNDULATOR, ELEMEDGE = 1.0e-2, K = 10.81, LAMBDA = 8.5e-2, NUMPERIODS = 10,
     MESHLENGTH = { 9e-3, 9e-3, 5e-3 }, MESHRESOLUTION = { 50e-6, 50e-6, 8e-6},
     TRUNORDER = 2, TOTALTIME = 5e-9, FNAME = "output_params.job";

DRIVE: Line = (UND);

//----------------------------------------------------------------------------
// INITIAL DISTRIBUTION

Dist: DISTRIBUTION, TYPE = MULTIGAUSS,
      SIGMAR = 1.0e-3,
      SIGMAZ = 90e-6 / 2.355, // FWHM  = 2.355 * sigma
      CUTOFFLONG = 4.0,  // In units of SIGZ
      SEPPEAKS = 200e-6,
      NPEAKS = 4,
      WRITETOFILE = FALSE,
      EMITTED = FALSE;

	
//----------------------------------------------------------------------------
// Define Field solvers
// The mesh sizes should be a factor of 2 
// for most efficient space charge calculation.

FS_SC: Fieldsolver, FSTYPE = FFT, 
            MX = 16, MY = 16, MT = 32,  // Grid for static solver
            PARFFTX = true,
            PARFFTY = true, 
            PARFFTT = true, 
            BCFFTX = open, 
            BCFFTY = open, 
            BCFFTT = open,
            BBOXINCR = 1, 
            GREENSF = INTEGRATED;

//----------------------------------------------------------------------------
// Electron Beam Definition
BEAM1:  BEAM, PARTICLE = ELECTRON, GAMMA = gamma, NPART = n_particles,
	BFREQ = 1, BCURRENT = beam_bunch_charge * 1e6 , CHARGE = -1;
//----------------------------------------------------------------------------
// Simulate the beamline using TRACK and RUN.

TRACK, LINE = DRIVE, BEAM = BEAM1, MAXSTEPS = 1000000, DT = 3.E-12, ZSTOP= 2.0 ;
RUN, METHOD = "PARALLEL-T", BEAM = BEAM1, 
    FIELDSOLVER = FS_SC, DISTRIBUTION = Dist;
ENDTRACK;

Quit;
